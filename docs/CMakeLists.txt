find_package(Doxygen REQUIRED)

# Find all the public headers
get_target_property(BIOVOLTRON_PUBLIC_HEADER_DIR biovoltron INTERFACE_INCLUDE_DIRECTORIES)
file(GLOB_RECURSE BIOVOLTRON_PUBLIC_HEADERS ${BIOVOLTRON_PUBLIC_HEADER_DIR}/*.hpp)

set(DOXYGEN_SOURCE_DIR ${PROJECT_SOURCE_DIR}/docs)
set(DOXYGEN_INPUT_DIR ${PROJECT_SOURCE_DIR}/include/biovoltron)
set(DOXYGEN_OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR}/doxygen)
set(DOXYGEN_INDEX_FILE ${DOXYGEN_OUTPUT_DIR}/xml/index.xml)
set(DOXYFILE_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(DOXYFILE_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

# Replace variables inside @@ with the current values
configure_file(${DOXYFILE_IN} ${DOXYFILE_OUT} @ONLY)

# Doxygen won't create this for us
file(MAKE_DIRECTORY ${DOXYGEN_OUTPUT_DIR})

# Only regenerate Doxygen when the Doxyfile or public headers change
add_custom_command(OUTPUT ${DOXYGEN_INDEX_FILE}
                   DEPENDS ${BIOVOLTRON_PUBLIC_HEADERS}
                   COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYFILE_OUT}
                   MAIN_DEPENDENCY ${DOXYFILE_OUT} ${DOXYFILE_IN}
                   COMMENT "Generating docs"
                   VERBATIM)

# Nice named target so we can run the job easily
add_custom_target(Doxygen ALL DEPENDS ${DOXYGEN_INDEX_FILE})
